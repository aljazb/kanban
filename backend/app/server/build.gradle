plugins {
    id "com.bmuschko.cargo" version "2.2.3"
}

repositories {
    maven {
        url 'https://repo.gradle.org/gradle/libs-releases'
    }
}

dependencies {

    testCompile project(':api')
    testCompile project(':core:businessLogic')

    testCompile group: 'junit', name: 'junit'

    testCompile 'org.jboss.shrinkwrap.resolver:shrinkwrap-resolver-bom:2.2.6'
    testCompile 'org.jboss.shrinkwrap.resolver:shrinkwrap-resolver-depchain:2.2.6'
    testCompile 'org.jboss.shrinkwrap.resolver:shrinkwrap-resolver-gradle-depchain:2.2.6'

    testCompile 'org.jboss.arquillian:arquillian-bom:1.1.13.Final'
    testCompile 'org.jboss.arquillian.junit:arquillian-junit-container:1.1.13.Final'

    testCompile "org.wildfly.arquillian:wildfly-arquillian-container-embedded:2.0.2.Final"

    def cargoVersion = '1.6.4'
    cargo "org.codehaus.cargo:cargo-core-uberjar:$cargoVersion",
            "org.codehaus.cargo:cargo-ant:$cargoVersion"
}

/**
 * Ear testing configuration
 */
ext {
    jbossHome = 'C:/Users/Targma/Servers/wildfly-10.0.0.Final'
    loggingManager = 'org.jboss.logmanager.LogManager'
    config = 'standalone-full.xml'
    modules = project.jbossHome + '/modules'
}

def setProperties() {
    evaluationDependsOn(":app:server:rest")
    def warProject = project(":app:server:rest")
    ext.contextRoot = warProject.warContextRoot

    evaluationDependsOn(":app:server:ear")
    def earProject = project(":app:server:ear")
    ext.earPath = earProject.buildDir.path + '/libs/' + 'ear-1.0-SNAPSHOT.ear'
}

setProperties()

test {
    environment 'JBOSS_HOME', project.jbossHome

    systemProperty 'ear.path', project.earPath
    systemProperty 'deployment.contextRoot', project.contextRoot

    systemProperty 'java.util.logging.manager', project.loggingManager
    systemProperty 'jboss.server.default.config', project.config
    systemProperty 'modulePath', project.modules
}

tasks.test.dependsOn(':app:server:ear:assemble')

cargo {
    containerId = 'wildfly10x'
    port = 8080
    deployable{
        file = file(project.projectDir.path + '/ear/build/libs/ear-1.0-SNAPSHOT.ear')
    }
    local {
        //outputFile = new File('out.log')
        homeDir = file(project.jbossHome)
        containerProperties {
            property 'cargo.jboss.configuration', 'standalone-full'
        }
    }
}

cargoRunLocal{
    dependsOn ':app:server:ear:ear'
}

cargoStartLocal{
    dependsOn ':app:server:ear:ear'
}